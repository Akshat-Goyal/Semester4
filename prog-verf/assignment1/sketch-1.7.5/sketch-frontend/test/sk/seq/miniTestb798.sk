pragma options "--bnd-inbits 2 --bnd-arr-size 4 --bnd-unroll-amnt 8 --bnd-inline-amnt 5";

// test condjoin of IntAbsValue with known ADT cases
// comprehensive test for ADT, StmtSwitch, and recursive generator inlining

#define DataType int

struct Vector {
	int len;
	DataType[len] data;
}

struct Opr {}

struct BroadCast extends Opr {
	DataType c;
}
generator BroadCast broadcast(DataType c) {
	return new BroadCast(c = c);
}

struct Lift extends Opr {
	Vector v;
}
generator Lift lift(Vector v) {
	return new Lift(v = v);
}

struct Plus extends Opr {
	Opr left;
	Opr right;
}
generator Plus plus(Opr left, Opr right) {
	return new Plus(left = left, right = right);
}

struct Minus extends Opr {
	Opr left;
	Opr right;
}
generator Minus minus(Opr left, Opr right) {
	return new Minus(left = left, right = right);
}

struct Times extends Opr {
	Opr left;
	DataType right;
}
generator Times times(Opr left, DataType right) {
	return new Times(left = left, right = right);
}

struct Dot extends Opr {
	Opr left;
	Opr right;
}
generator Dot dot(Opr left, Opr right) {
	return new Dot(left = left, right = right);
}

struct L2norm extends Opr {
	Opr v;
}

generator DataType eval(Opr op, int i) {
	switch(op) {
	case BroadCast:
		return op.c;
	case Lift:
		return op.v.data[i];
	case Plus:
		return eval(op.left, i) + eval(op.right, i);
	case Minus:
		return eval(op.left, i) - eval(op.right, i);
	case Times:
		return eval(op.left, i) * op.right;
	case Dot:
		return eval(op.left, i) * eval(op.right, i);
	case L2norm:
		assert 0 : "This should #NOT# occur";
	}
}

generator void assign(Vector lhs, Opr rhs) {
	for (int i = 0; i < lhs.len; ++i) {
		lhs.data[i] = eval(rhs, i);
	}
}

DataType[len] f(int len, DataType[len] x, DataType[len] y, DataType alpha, DataType beta, DataType gamma) {
	DataType[len] r;
	for (int i = 0; i < len; ++i) {
		DataType Y;
		if (len % 2 == 0) {
			Y = y[i];
		} else {
			Y = x[i];
		}
		DataType a = x[i] * Y, b = x[i] * alpha, c = Y * beta;
		DataType d;
		if (len % 3 == 0) {
			d = a + b;
		} else {
			d = a - b;
		}
		DataType e = d - c;
		if (len % 3 == 0) {
			e = -e;
		}
		DataType f = e - gamma;
		if (len % 2 == 0) {
			f = gamma + e;
		}
		r[i] = f;
	}
	return r;
}

DataType[len] g(int len, DataType[len] x, DataType[len] y, DataType alpha, DataType beta, DataType gamma) implements f {
	DataType[len] r;
	Vector result = new Vector(len = len, data = r);
	Opr X = lift(new Vector(len = len, data = x));
	Opr Y;
	if (len % 2 == 0) {
		Y = lift(new Vector(len = len, data = y));
	} else {
		Y = lift(new Vector(len = len, data = x));
	}
	Opr Gamma = broadcast(gamma);
	Opr a = dot(X, Y);
	Opr b = times(X, alpha), c = times(Y, beta);
	Opr d;
	if (len % 3 == 0) {
		d = plus(a, b);
	} else {
		d = minus(a, b);
	}
	Opr e = minus(d, c);
	if (len % 3 == 0) {
		e = minus(c, d);
	}
	Opr f = minus(e, Gamma);
	if (len % 2 == 0) {
		f = plus(Gamma, e);
	}
	assign(result, f);
	return result.data;
}