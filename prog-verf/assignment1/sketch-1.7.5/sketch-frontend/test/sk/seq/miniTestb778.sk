//@Description This sketch derives the desugar function between two simple toy languages.

/**
 * Data definitions.
 */
adt srcAST {
	NumS{ int v;}
	PlusS{ srcAST a; srcAST b;}
	MinusS{ srcAST a; srcAST b;}
	TrueS{}
	FalseS{}
}

adt dstAST{
	NumD{ int v;}
	BinopD{ opcode op; dstAST a; dstAST b;}
	BoolD{ bit val;}
}

adt opcode{
	PlusOp{}
	MinusOp{}
}

/**
 * Function to synthesize.
 */
dstAST desugar(srcAST src){
	if (src == null) return null;
	switch(src) {
		repeat_case: {
			return new ??( a= desugar(src.??), b= desugar(src.??),
			 op= ??, v=src.??, val=??);
		}
	}
}


/**
 * Test harnesses
 */
harness void main1() {
	srcAST in = new FalseS();
	dstAST out  = desugar(in);
	dstAST exp = new BoolD(val = 0);
	assert exp === out;
}
harness void main2() {
	srcAST in = new TrueS();
	dstAST out  = desugar(in);
	dstAST exp = new BoolD(val = 1);
	assert exp === out;
}
harness void main3(int x1, int x2) {
	srcAST in = new MinusS(a=new NumS(v=x1), b=new NumS(v=x2));
	dstAST out = desugar(in);
	dstAST exp = new BinopD( op = new MinusOp(), a=new NumD(v=x1), b=new NumD(v=x2)); 
	assert exp === out;
}
harness void main4(int x1, int x2) {
	srcAST in = new PlusS(a=new NumS(v=x1), b=new NumS(v=x2));
	dstAST out = desugar(in);
	dstAST exp = new BinopD( op = new PlusOp(), a=new NumD(v=x1), b=new NumD(v=x2)); 
	assert exp === out;
}